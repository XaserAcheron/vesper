/*****************************************************************************
 * vesper - decohack - ichor rifle
 * (c) xaser acheron 2021
 *     MIT License
 ****************************************************************************/

weapon Ichor "Ichor Rifle"
{
	ammotype AM_CELL
	ammopershot 3
	clear states
	states
	{
		ready:
			PLSG AAAAAAAAABBBCCCDDDEEE 1 A_WeaponReady
			loop
		select:
			PLSG A 0 A_Raise
			PLSG A 1 A_Raise
			loop
		deselect:
			PLSG A 0 A_Lower
			PLSG A 1 A_Lower
			loop

		/* flash states */

		flash1:
			PLSF A 2 Bright A_Light1
		flashdone:
			PLSF C 2 Bright A_Light1
			goto lightdone

		flash2:
			PLSF B 2 Bright A_Light2
			goto flashdone

		/* fire states */

		fire:
			PLSG A 0 A_WeaponProjectile(thing IchorShot, 0.0, 0.0, 0.0, -2.0)
			PLSG A 0 A_ConsumeAmmo(1)
			PLSG A 0 A_GunFlashTo(flash1)
			PLSG A 1 Offset(1, 36) A_WeaponSound(sound DSICHFIR)
			PLSG A 1 Offset(3, 40)
			PLSG A 1 Offset(2, 38)

			PLSG A 0 A_WeaponProjectile(thing IchorShot, 0.0, 0.0, 0.0, -2.0)
			PLSG A 0 A_ConsumeAmmo(1)
			PLSG A 0 A_GunFlashTo(flash2)
			PLSG A 1 Offset(1, 36) A_WeaponSound(sound DSICHFIR)
			PLSG A 1 Offset(-1, 40)
			PLSG A 1 Offset(-1, 38)

			PLSG A 0 A_WeaponProjectile(thing IchorShot, 0.0, 0.0, 0.0, -2.0)
			PLSG A 0 A_ConsumeAmmo(1)
			PLSG A 0 A_GunFlashTo(flash1)
			PLSG A 1 Offset(1, 36) A_WeaponSound(sound DSICHFIR)
			PLSG A 1 Offset(1, 40)
			PLSG A 1 Offset(1, 38)

			PLSG C 2 Offset(1, 36)
			PLSG DE 2
			PLSG A 0 A_Refire
			PLSG F 2 A_WeaponSound(sound DSICHREL)
			PLSG G 2
			PLSG HIJK 3
			PLSG A 2 Offset(1, 35)
			PLSG A 2 Offset(1, 33)
			PLSG A 2 Offset(1, 32)
			goto ready
	}
}

thing IchorShot "Ichor Bolt"
{
	Damage ICHOR_DAMAGE
	Speed ICHOR_VELOCITY
	Radius 11
	Height 8

	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+MISSILE
	+TRANSLUCENT

	SeeSound DSICHPRJ
	DeathSound DSICHHIT

	states
	{
		spawn:
			ICHR AAAA 1 Bright A_SpawnObject(thing IchorTrail)
			ICHR A 0 Bright A_FindTracer(ICHOR_SEEK_FOV)
		tryseek:
			ICHR A 0 Bright A_JumpIfTracerInSight(seek)
			goto spawn
		seek:
			ICHR A 0 Bright A_SpawnObject(thing IchorTrail)
			ICHR A 1 Bright A_SeekTracer(ICHOR_SEEK_THRESHOLD, ICHOR_SEEK_ANGLE)
			// in deathmatch, don't pick a new target if initial target dies
			#ifdef EXT_DEATHMATCH
				loop
			#else
				goto tryseek
			#endif
		death:
			ICHR H 4 Bright A_AddFlags(TRANSLUCENT, 0)
			ICHR IJKLM 4 Bright
			stop
	}
}

thing IchorTrail "Ichor Trail"
{
	Radius 4
	Height 8

	+NOBLOCKMAP
	+NOGRAVITY
	+TRANSLUCENT

	states
	{
	spawn:
		TNT1 A 1 Bright
		ICHR CDEFG 1 Bright
		stop
	}
}
