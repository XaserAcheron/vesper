/*****************************************************************************
 * vesper - decohack - vintage eagle
 * (c) xaser acheron 2021
 *     MIT License
 ****************************************************************************/

weapon WP_PISTOL free states
weapon WP_PISTOL "Vintage Eagle"
{
	ammotype AM_CLIP
	clear states
	states
	{
		ready:
			PISG A 1 A_WeaponReady
			wait
		select:
			PISG A 0 A_Raise
			PISG A 1 A_Raise
			loop
		deselect:
			PISG A 0 A_Lower
			PISG A 1 A_Lower
			loop
		slowfire:
			PISG A 1 Offset(1, 34)
			PISG A 1 Offset(1, 33)
			PISG A 4
			PISG A 1 A_Refire
			goto ready
		fire:
			PISG A 0 A_GunFlash
			PISG A 0 A_ConsumeAmmo
			PISG A 0 A_WeaponSound(SND_MAGNUM, 0)
			PISG A 1 A_WeaponBulletAttack(MAGNUM_DAMAGE, MAGNUM_SPREAD)
			PISG B 1
			PISG B 1 Offset(1, 36)
			PISG C 1 Offset(1, 44)
			PISG C 1 Offset(1, 48)
			PISG D 1 Offset(1, 50)
			PISG D 1 Offset(1, 51)
			PISG E 1 Offset(1, 51)
			PISG E 1 Offset(1, 50)
			PISG F 1 Offset(1, 48)
			PISG F 1 Offset(1, 44)
			PISG A 1 Offset(1, 40)
			PISG A 1 Offset(1, 36)
			PISG A 0 A_RefireTo(weapon WP_PISTOL slowfire)
			goto ready
		flash:
			PISF A 2 Bright A_Light1
			goto lightdone
	}
}
