/*****************************************************************************
 * vesper - decohack - heavy spiker
 * (c) xaser acheron 2021
 *     MIT License
 ****************************************************************************/

weapon WP_CHAINGUN "Heavy Spiker"
{
	ammotype AM_CLIP
	ammopershot 3
	clear states
	states
	{
		ready:
			CHGG A 1 A_WeaponReady
			wait
		select:
			CHGG A 0 A_Raise
			CHGG A 1 A_Raise
			loop
		deselect:
			CHGG A 0 A_Lower
			CHGG A 1 A_Lower
			loop

		/* flash states */

		flash1:
			CHGF A 2 Bright A_Light1
			goto lightdone
		flash2:
			CHGF B 2 Bright A_Light2
			goto lightdone

		/* fire states */

		fire:
			CHGG A 1 Offset(-1, 36)
			CHGG A 1 Offset(-1, 38)
			CHGG A 1 Offset(-1, 40)
		fire1:
			CHGG A 0 A_WeaponProjectile(DEHNUM_SPIKE_SHOT_LEFT, 0.0, 0.0, 0.0, -2.0)
			CHGG A 0 A_ConsumeAmmo(1)
			CHGG A 0 A_GunFlashTo(weapon WP_CHAINGUN flash1, 0)
			CHGG A 1 Offset(-1, 44) A_WeaponSound(sound DSSPIKE1, 0)
			CHGG B 1 Offset(-1, 42)
			CHGG C 1 Offset(-1, 41)
			CHGG D 1 Offset(-1, 40)
			CHGG A 1 Offset(-1, 39)
		fire2:
			CHGG A 0 A_WeaponProjectile(DEHNUM_SPIKE_SHOT_LEFT , 0.0, 0.0, -4.0, -2.0)
			CHGG A 0 A_WeaponProjectile(DEHNUM_SPIKE_SHOT_RIGHT, 0.0, 0.0,  4.0, -2.0)
			CHGG A 0 A_ConsumeAmmo(2)
			CHGG A 0 A_GunFlashTo(weapon WP_CHAINGUN flash2, 0)
			CHGG A 1 Offset(-1, 44) A_WeaponSound(sound DSSPIKE2, 0)
			CHGG B 1 Offset(-1, 42)
			CHGG C 1 Offset(-1, 41)
			CHGG D 1 Offset(-1, 40)
			CHGG A 1 Offset(-1, 39)
		fireend:
			CHGG A 1 Offset(-1, 38) A_ReFireTo(weapon WP_CHAINGUN fire1, 0)
			CHGG A 1 Offset(-1, 36)
			goto ready
	}
}

thing DEHNUM_SPIKE_SHOT_LEFT "Spike Shot (Left)"
{
	Damage SPIKE_DAMAGE
	Speed SPIKE_VELOCITY
	Radius 11
	Height 8

	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+MISSILE

	SeeSound DSSPKFIR
	DeathSound DSSPKHIT

	states
	{
		spawn:
			SPIK A 1 Bright A_Spawn(DEHNUM_SPIKE_TRAIL, 0)
			wait
		death:
			SPIK B 3 A_AddFlags(TRANSLUCENT, 0)
			SPIK CDEFG 3
			stop
	}
}

thing DEHNUM_SPIKE_SHOT_RIGHT : thing DEHNUM_SPIKE_SHOT_LEFT "Spike Shot (right)"
{
	SeeSound ""
}

thing DEHNUM_SPIKE_TRAIL "Spike Trail"
{
	Radius 4
	Height 8

	+NOGRAVITY
	+NOBLOCKMAP
	+TRANSLUCENT

	states
	{
	spawn:
		TNT1 A 1 Bright
		SPIK HIJKLMNO 1 Bright
		stop
	}
}
