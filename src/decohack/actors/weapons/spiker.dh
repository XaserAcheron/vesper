/*****************************************************************************
 * vesper - decohack - heavy spiker
 * (c) xaser acheron 2021
 *     MIT License
 ****************************************************************************/

state free S_CHAINFLASH2

weapon WP_CHAINGUN free states
weapon WP_CHAINGUN "Heavy Spiker"
{
	ammotype AM_CLIP
	clear states
	states
	{
		ready:
			CHGG A 0 A_ReFire
			CHGG A 1 A_WeaponReady
			loop
		select:
			CHGG A 0 A_Raise
			CHGG A 1 A_Raise
			loop
		deselect:
			CHGG A 0 A_Lower
			CHGG A 1 A_Lower
			loop

		/* flash states */

		flash1:
			CHGF A 2 Bright A_Light1
			goto lightdone
		flash2:
			CHGF B 2 Bright A_Light2
			goto lightdone

		/* fire states */

		fire:
			CHGG A 1 Offset(-1, 36)
			CHGG A 1 Offset(-1, 38)
			CHGG A 1 Offset(-1, 40)
		fire1:
			CHGG A 0 A_WeaponProjectile(DEHNUM_SPIKE_SHOT_SINGLE, 0.0)
			CHGG A 0 A_ConsumeAmmo(1)
			CHGG A 0 A_GunFlashTo(weapon WP_CHAINGUN flash1, 0)
			CHGG A 1 Offset(-1, 44) A_WeaponSound(SND_SPIKE1, 0)
			CHGG B 1 Offset(-1, 42)
			CHGG C 1 Offset(-1, 41)
			CHGG D 1 Offset(-1, 40)
		fire2:
			CHGG A 0 A_WeaponProjectile(DEHNUM_SPIKE_SHOT_DOUBLE, 0.0)
			CHGG A 0 A_ConsumeAmmo(2)
			CHGG A 0 A_GunFlashTo(weapon WP_CHAINGUN flash2, 0)
			CHGG A 1 Offset(-1, 44) A_WeaponSound(SND_SPIKE2, 0)
			CHGG B 1 Offset(-1, 42)
			CHGG C 1 Offset(-1, 41)
			CHGG D 1 Offset(-1, 40)
			CHGG A 0 A_ReFireTo(weapon WP_CHAINGUN fire1, 0)
		fireend:
			CHGG A 1 Offset(-1, 40)
			CHGG A 1 Offset(-1, 38)
			CHGG A 1 Offset(-1, 36)
			goto ready
	}
}

thing DEHNUM_SPIKE_SHOT_SINGLE "Spike Shot (single)"
{
	Damage SPIKE_SINGLE_DAMAGE
	Speed SPIKE_VELOCITY
	Radius 11
	Height 8

	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+MISSILE

	SeeSound DSSPKFIR
	DeathSound DSSPKHIT

	states
	{
		spawn:
			SPK1 A 1 Bright A_Spawn(DEHNUM_SPIKE_TRAIL_SINGLE, 0)
			loop
		death:
			SPK1 BCDEFG 3
			stop
	}
}

thing DEHNUM_SPIKE_SHOT_DOUBLE : thing DEHNUM_SPIKE_SHOT_SINGLE "Spike Shot (double)"
{
	Damage SPIKE_DOUBLE_DAMAGE

	states
	{
		spawn:
			SPK2 A 1 Bright A_Spawn(DEHNUM_SPIKE_TRAIL_DOUBLE, 0)
			loop
		death:
			SPK2 BCDEFG 3
			stop
	}
}

thing DEHNUM_SPIKE_TRAIL_SINGLE "Spike Trail (single)"
{
	Radius 4
	Height 8

	+NOGRAVITY
	+NOBLOCKMAP
	+TRANSLUCENT

	states
	{
	spawn:
		TNT1 A 1 Bright
		SPK1 HIJKLMNO 1 Bright
		stop
	}
}

thing DEHNUM_SPIKE_TRAIL_DOUBLE : thing DEHNUM_SPIKE_TRAIL_SINGLE "Spike Trail (double)"
{
	states
	{
	spawn:
		TNT1 A 1 Bright
		SPK2 HIJKLMNO 1 Bright
		stop
	}
}
