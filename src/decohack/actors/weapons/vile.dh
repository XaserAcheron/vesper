/*****************************************************************************
 * vesper - decohack - vile driver
 * (c) xaser acheron 2021
 *     MIT License
 ****************************************************************************/

weapon WP_BFG free states
weapon WP_BFG "Vile Driver"
{
	ammotype AM_CELL
	ammopershot 6

	-NOAUTOFIRE

	clear states
	states
	{
		ready:
			BFGG AAAAA 1 A_WeaponReady
			BFGG A 0 A_CheckAmmo(ready)
		willing:
			BFGF A 0 A_WeaponSound(sound DSVIDACT)
		able:
			BFGF A 1 A_WeaponReady
			loop
		select:
			BFGG A 0 A_Raise
			BFGG A 1 A_Raise
			loop
		deselect:
			BFGG A 0 A_Lower
			BFGG A 1 A_Lower
			loop
		dryfire:
			BFGG F 1 A_WeaponSound(sound DSVIDDRY)
			BFGG GHFGHFGH 1
			BFGG H 1
			BFGG H 0 A_RefireTo(dryfire, 1)
			BFGG F 1 A_WeaponSound(sound DSVIDDWN)
			BFGG GHFGH 1
			BFGG FGHFGHIJK 2
			BFGG LM 3
			goto ready
		fire:
			BFGF B 3 Offset(1, 32) A_WeaponSound(sound DSVIDUP)
			BFGF CDEFGHFGH 2
			BFGF FGHFGHFGH 1
			BFGF I 0 Bright A_WeaponSound(sound DSVIDSTR)
		hold:
			BFGF I 0 Bright A_WeaponProjectile(XTF_VILE_SHOT_1, -3.078,-1.000, 0.0, -2.0)
			BFGF I 0 Bright A_WeaponProjectile(XTF_VILE_SHOT_2,  3.078,-1.000, 0.0, -2.0)
			BFGF I 0 Bright A_ConsumeAmmo(2)
			BFGF I 1 Bright Offset(1, 32) A_GunFlash()
			BFGF J 1 Bright Offset(3, 34)
			BFGF K 1 Bright Offset(2, 33)
			BFGF I 0 Bright A_WeaponProjectile(XTF_VILE_SHOT_1,  0.000,-3.236, 0.0, -2.0)
			BFGF I 0 Bright A_WeaponProjectile(XTF_VILE_SHOT_2,  2.618, 1.902, 0.0, -2.0)
			BFGF I 0 Bright A_ConsumeAmmo(2)
			BFGF I 1 Bright Offset( 1, 32) A_GunFlash()
			BFGF J 1 Bright Offset(-1, 34)
			BFGF K 1 Bright Offset(-1, 33)
			BFGF I 0 Bright A_WeaponProjectile(XTF_VILE_SHOT_1,  0.000,-3.236, 0.0, -2.0)
			BFGF I 0 Bright A_WeaponProjectile(XTF_VILE_SHOT_2, -2.618, 1.902, 0.0, -2.0)
			BFGF I 0 Bright A_ConsumeAmmo(2)
			BFGF I 1 Bright Offset(1, 32) A_GunFlash()
			BFGF J 1 Bright Offset(1, 34)
			BFGF K 1 Bright Offset(1, 33)
			BFGF K 0 Bright Offset(1, 32) A_WeaponSound(sound DSVIDSPN)
			BFGF K 0 Bright A_CheckAmmo(dryfire)
			BFGF K 0 Bright A_RefireTo(hold, 1)
		spindown:
			BFGF F 1 A_WeaponSound(sound DSVIDDWN)
			BFGF GHFGH 1
			BFGF FGHFGHLMN 2
			BFGF OP 3
			goto able
		flash:
			TNT1 A 2 Bright A_Light1
			TNT1 A 1 Bright A_Light2
			goto lightdone
	}
}

thing XTF_VILE_SHOT_1 "Vile Bolt"
{
	Damage VILE_DAMAGE
	Speed VILE_VELOCITY
	Radius 11
	Height 8

	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+MISSILE
	+TRANSLUCENT

	SeeSound DSVIDFIR
	DeathSound DSVIDHIT

	states
	{
		seek:
			BILE A 0 Bright A_SpawnObject(XTF_VILE_TRAIL)
			BILE A 1 Bright A_SeekTracer(VILE_SEEK_THRESHOLD, VILE_SEEK_ANGLE)
		tryseek:
			BILE A 0 Bright A_JumpIfTracerInSight(seek)
		spawn:
			BILE AAAAAAAA 1 Bright A_SpawnObject(XTF_VILE_TRAIL)
			BILE A 0 Bright A_FindTracer(VILE_SEEK_FOV)
			goto tryseek
		death:
			BILE H 4 Bright A_AddFlags(TRANSLUCENT, 0)
			BILE IJKLM 4 Bright
			stop
	}
}

thing XTF_VILE_SHOT_2 : thing XTF_VILE_SHOT_1 "Vile Bolt (Quiet)"
{
	SeeSound ""
}

thing XTF_VILE_TRAIL "Vile Trail"
{
	Radius 4
	Height 8

	+NOBLOCKMAP
	+NOGRAVITY
	+TRANSLUCENT

	states
	{
	spawn:
		TNT1 A 1 Bright
		BILE CDEFG 1 Bright
		stop
	}
}
